buildscript {
    dependencies {
        classpath files('gradle/gradle-witness.jar')
    }
}

apply plugin: 'java'
apply plugin: 'witness'
apply plugin: 'maven-publish'

compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

version = "0.4.7m"

repositories {
    mavenLocal()
    maven { url "http://4thline.org/m2" }
    maven { url 'https://mvn.freenetproject.org' }
    jcenter()
    maven { url 'https://jitpack.io' }
}

configurations {
    extraLibs
}

dependencies {
    compile group: 'net.java', name: 'textile-j', version: '2.2'
    compile group: 'com.github.freenet-mobile', name: 'fred', version: '1486.5m'
    extraLibs group: 'org.fourthline.cling', name: 'cling-core', version: '2.1.0'
    extraLibs group: 'org.fourthline.cling', name: 'cling-support', version: '2.1.0'
    configurations.compile.extendsFrom(configurations.extraLibs)

}

dependencyVerification {
    verify = [
            'org.fourthline.cling:cling-core:cf556bae1a8526626b139e1a3815a8c960c4c55bf1d24baae143ff8079f326fd',
            'org.fourthline.cling:cling-support:d090bff05f033f2d0d1da42ba9f367d1155b54af3b44237baddd0faeef5f6a1b',
            'org.seamless:seamless-util:eb663e3739d67137baab18e65ed2cdec28213a8871458323c3cc62da085cec3c',
            'org.seamless:seamless-http:05da30fa260cf53770fefbd46482c04f6a37e19b663fec282a4c4384c0def813',
            'org.seamless:seamless-xml:6d80a97918e4ae91ecb676f9cd056942f1565d981d054a461058a16096464298',
            'org.seamless:seamless-swing:653fa6fea357f1349075bdd94328fc9c0d285046d7deb25a56dc8a86513b64e0',
    ]
}

import java.nio.file.Files;
import java.nio.file.Path;
import java.nio.file.Paths;
import java.nio.file.attribute.BasicFileAttributes;

Date getMTime(String file) {
	return new Date(Files.readAttributes(Paths.get(file), BasicFileAttributes.class).lastModifiedTime().toMillis());
}

jar {
    manifest {
        attributes 'Plugin-Main-Class': 'plugins.Sharesite.Plugin',
                   'Required-Node-Version': '1472',
                   'Implementation-Version': version,
                   'Built-By': System.getProperty('user.name'),
                   'Built-Date': getMTime("src/main/java/plugins/Sharesite/Plugin.java"),
                   'Built-JDK': System.getProperty('java.version')
    }
    from (configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }

    from("src/main/java") {
        include "**/*.l10n"
        include "**/*.png"
        include "**/*.txt"
        include "**/*.html"
        include "**/*.css"
    }

    preserveFileTimestamps = false
    reproducibleFileOrder = true
    duplicatesStrategy = "exclude"
    archiveName = 'freenet-Sharesite.jar'
}

def jars = []
gradle.addListener(new TaskExecutionListener() {
void afterExecute(Task task, TaskState state) {
    if(task in AbstractArchiveTask) {
        jars << task.outputs.files.singleFile
    }
}

void beforeExecute(Task task) { }
})
gradle.addBuildListener(new BuildAdapter() {
    void buildFinished(BuildResult result) {
        if(jars) {
            def hash = {
                File file -> def sha256 = java.security.MessageDigest.getInstance('SHA-256')
                file.eachByte(1024 * 4) { buffer, len -> sha256.update(buffer, 0, len) }
                println "SHA-256 of ${file.name}: ${sha256.digest().encodeHex().toString()}"
            }

            jars.each { hash(it) }
        }
    }
})

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'org.freenetproject.plugins'
            artifactId "Sharesite"
            version version
            from components.java
        }
    }
    repositories {
        maven {
            url "s3://mvn.freenetproject.org/"
            credentials(AwsCredentials) {
                accessKey System.getenv('AWS_ACCESS_KEY_ID')
                secretKey System.getenv('AWS_SECRET_ACCESS_KEY')
            }
        }
    }
}
